@Metadata.layer: #CORE
@UI:{
    headerInfo: {
        typeName: 'Billing Document Header',
        typeNamePlural: 'Billing Documents',
        title: {
        type: #STANDARD,
        label: 'Billing Document',
        value: 'BillID' }
    }
}
annotate entity ZVIE_C_BILL_HEAD with
{
  @UI.facet: [
  {
                id: 'BillingDocHead',
                purpose: #STANDARD,
                type: #IDENTIFICATION_REFERENCE,
                label: 'Header',
                position: 10
                },

                {
                id: 'BillingDocItem',
                purpose: #STANDARD,
                type: #LINEITEM_REFERENCE,
                label: 'Items',
                position: 20,
                targetElement: '_item'
                }


                ]

  @EndUserText.label: 'Billing Document'
  @UI: { lineItem: [{ position: 10 },
                    { position: 10, type: #FOR_ACTION, dataAction: 'updateBillingDate', label: 'Reset Billing Date' },
                    { position: 20, type: #FOR_ACTION, dataAction: 'createBillingDocHeader', label: 'Create Bill Header' },
                    { position: 30, type: #FOR_ACTION, dataAction: 'copyBillingDocument', label: 'Copy Billing Document' }
                    ],
         identification: [{ position: 10 }],
         selectionField: [{ position: 10 }] }
  BillId;
  @EndUserText.label: 'Billing Document Typee'
  @UI: { lineItem: [{ position: 20 }],
         identification: [{ position: 20 }],
         selectionField: [{ position: 20 }] }
  //         @Consumption.valueHelpDefinition: [{ entity: { element: 'BillingDocumentType', name: 'I_BillingDocumentType' } }]
  BillType;
  @EndUserText.label: 'Billing Date'
  @UI: { lineItem: [{ position: 30 }],
         identification: [{ position: 30 }] }
  BillDate;
  @EndUserText.label: 'Customer'
  @UI: { lineItem: [{ position: 40 }],
         identification: [{ position: 40 }] }
  CustomerId;
  @EndUserText.label: 'Net Amount'
  @UI: { lineItem: [{ position: 50 }],
         identification: [{ position: 50 }] }
  NetAmount;
  @EndUserText.label: 'Currency'
  @UI: { lineItem: [{ position: 60 }],
         identification: [{ position: 60 }] }
  Currency;
  @EndUserText.label: 'Sales Organization'
  @UI: { lineItem: [{ position: 70 }],
         identification: [{ position: 70 }] }
  SalesOrg;
  @UI: {
    lineItem: [
    {  hidden: true,
    exclude: true },
        {
        type: #AS_FIELDGROUP,
          valueQualifier: 'StartEnd',
          label: 'Planned Start / End' }
    ],
        fieldGroup: [
        {
        qualifier: 'StartEnd',
          position: 10 }]
    }
  @Consumption:{
      filter.hidden: true
       }
  PlanStartDate;
  @UI: {
    lineItem: [
    { hidden: true,
    exclude: true }
    ],
    fieldGroup: [
    {
    qualifier: 'StartEnd',
      position: 20 }] }
  @Consumption:{
    filter.hidden: true
     }
  PlanEndtDate;

}